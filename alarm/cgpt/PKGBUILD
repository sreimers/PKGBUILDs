# Maintainer: Justin Settle <jus10 at partlycloudy dot org>

buildarch=4

pkgname=cgpt
pkgdesc="Chromium OS cgpt partition tool"
pkgver=20131109
pkgrel=1
arch=('any')
url="https://chromium.googlesource.com/chromiumos/platform/vboot_reference/+/master"
license=('custom:chromiumos')
_commit=('a3d70a3d2b5c052db039d097aaffa42008da24b5')
source=('vboot-utils-00-disable-static-linking.patch'
	"https://chromium.googlesource.com/chromiumos/platform/vboot_reference/+archive/${_commit}.tar.gz")
noextract=("${_commit}.tar.gz")
md5sums=('0bef8d9991e3abaa9ea971a74c261f2a'
         'a92d51fb8e34d9a995e810957d828caa')

prepare() {
  mkdir -p "${srcdir}/vboot_reference"

  tar -xf "${_commit}.tar.gz" -C "${srcdir}/vboot_reference"

  cd "${srcdir}/vboot_reference"

  patch -p0 < "${srcdir}/vboot-utils-00-disable-static-linking.patch"
}

build() {
  cd "${srcdir}/vboot_reference"

  ARCH=arm make cgpt
}

package() {
  install -d "${pkgdir}/usr/sbin/"

  install -m755 "${srcdir}/vboot_reference/build/cgpt/cgpt" "${pkgdir}/usr/sbin/"	
  install -m 644 -D ${srcdir}/vboot_reference/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
