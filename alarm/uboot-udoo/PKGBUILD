# U-Boot: UDOO
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-udoo
pkgname=('uboot-udoo-dual' 'uboot-udoo-quad')
pkgver=2009.08
pkgrel=2
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git')
option=('!strip')
source=("uboot::git+https://github.com/UDOOboard/U-Boot_Unico.git"
        'ext4.patch'
        'alarm.patch')
md5sums=('SKIP'
         'a53b7e683b293ea36945a4d45502ca88'
         '1ff033040c597830951d7d11e487761c')

prepare() {
  cd uboot
  patch -p1 -i "${srcdir}"/ext4.patch
  patch -p1 -i "${srcdir}"/alarm.patch
  rm -rf .git
}

build() {
  cd uboot
  
  unset CFLAGS
  unset CXXFLAGS
  
  # Dual
  make distclean
  make mx6dl_udoo_config
  make DDR_SIZE=2 DDR_TYPE=1 BOARD_TYPE=UDOO CPU_TYPE=DUAL_LITE OS_TYPE=LINUX ENV_DEVICE=ENV_MMC
  mv u-boot.bin u-boot-dual.imx

  # Quad
  make distclean
  make mx6q_udoo_config
  make DDR_SIZE=2 DDR_TYPE=1 BOARD_TYPE=UDOO CPU_TYPE=QUAD OS_TYPE=LINUX ENV_DEVICE=ENV_MMC
  mv u-boot.bin u-boot-quad.imx
}

package_uboot-udoo-dual() {
  pkgdesc="U-Boot for UDOO Dual"
  conflicts=('uboot-udoo-quad')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp uboot/u-boot-dual.imx "${pkgdir}"/boot/u-boot.bin
}

package_uboot-udoo-quad() {
  pkgdesc="U-Boot for UDOO Quad"
  conflicts=('uboot-udoo-dual')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp uboot/u-boot-quad.imx "${pkgdir}"/boot/u-boot.bin
}
